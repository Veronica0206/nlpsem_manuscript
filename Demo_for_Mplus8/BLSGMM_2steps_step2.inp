TITLE:      BLSGMM with an Unknown Fixed Knot
Data:       FILE = example_data.csv;
VARIABLE:   NAMES = id Y1-Y10 T1-T10 X1 X2; 
            USEVAR = Y1-Y10 X1 X2;
            CONSTRAINT = T1-T10;
            CLASSES = c(2);
ANALYSIS:   TYPE = MIXTURE;
            ALGORITHM = EM;
! We first estimate the reparameterized the mean vector and var-cov matrix of LVs
!(i.e., the measure at the knot, the mean and the half difference of two slopes). 
! mueta0s, mueta1s, and mueta2s are the means of such LVs;
! psi0s0s, psi1s1s, and psi2s2s are the variances of such LVs;
! psi0s1s, psi0s2s, and psi1s2s are the covariates of such LVs. 
! theta is the residual variance
MODEL:      
            %OVERALL%
            eta0s BY Y1-Y10@1;
            ! factor loadings for eta0s are set equal to 1
            eta1s BY Y1* (L11)
                     Y2* (L12)
                     Y3* (L13)
                     Y4* (L14)
                     Y5* (L15)
                     Y6* (L16)
                     Y7* (L17)
                     Y8* (L18)
                     Y9* (L19)
                     Y10* (L110);
            eta2s BY Y1* (L21)
                     Y2* (L22)
                     Y3* (L23)
                     Y4* (L24)
                     Y5* (L25)
                     Y6* (L26)
                     Y7* (L27)
                     Y8* (L28)
                     Y9* (L29)
                     Y10* (L210);
            c on X1 X2;
            
            %c#1%
            ! Factor loadings for eta1s and eta2s are not fixed values,
            ! because they contain an estimate parameter, the fixed knot

            ! Population values of parameters:
            !! Original settings:
            !! mueta0: 98, mueta1: -5, mueta2: -2.6, mug: 3.5
            !! psi00: 25, psi01: 1.5, psi02: 1.5
            !!            psi11: 1.0, psi12: 0.3
            !!                        psi22: 1.0

            !! Reparameterized settings:
            !! mueta0s: 80.5, mueta1s: -3.8, mueta2s: 1.2, mug: 3.5
            !! psi0s0s: 47.750, psi0s1s: 3.775, psi0s2s: -1.225
            !!                  psi1s1s: 0.650, psi1s2s: 0.000
            !!                                  psi2s2s: 0.350
            
            !! theta: 1.0
            eta0s BY Y1-Y10@1;
            ! factor loadings for eta0s are set equal to 1
            eta1s BY Y1* (c1L11)
                     Y2* (c1L12)
                     Y3* (c1L13)
                     Y4* (c1L14)
                     Y5* (c1L15)
                     Y6* (c1L16)
                     Y7* (c1L17)
                     Y8* (c1L18)
                     Y9* (c1L19)
                     Y10* (c1L110);
            eta2s BY Y1* (c1L21)
                     Y2* (c1L22)
                     Y3* (c1L23)
                     Y4* (c1L24)
                     Y5* (c1L25)
                     Y6* (c1L26)
                     Y7* (c1L27)
                     Y8* (c1L28)
                     Y9* (c1L29)
                     Y10* (c1L210);
      
            eta0s@46.270;
            eta1s@0.590;
            eta2s@0.368;

            eta0s WITH eta1s@3.406;
            eta0s WITH eta2s@-1.388;
            eta1s WITH eta2s@0.014;

            [eta0s@81.005];
            [eta1s@-3.776];
            [eta2s@1.157];

            Y1-Y10@1.097;
            [Y1-Y10@0];

            %c#2%
            eta0s BY Y1-Y10@1;
            ! factor loadings for eta0s are set equal to 1
            eta1s BY Y1* (c2L11)
                     Y2* (c2L12)
                     Y3* (c2L13)
                     Y4* (c2L14)
                     Y5* (c2L15)
                     Y6* (c2L16)
                     Y7* (c2L17)
                     Y8* (c2L18)
                     Y9* (c2L19)
                     Y10* (c2L110);
            eta2s BY Y1* (c2L21)
                     Y2* (c2L22)
                     Y3* (c2L23)
                     Y4* (c2L24)
                     Y5* (c2L25)
                     Y6* (c2L26)
                     Y7* (c2L27)
                     Y8* (c2L28)
                     Y9* (c2L29)
                     Y10* (c2L210);
            
            ! Factor loadings for eta1s and eta2s are not fixed values,
            ! because they contain an estimate parameter, the fixed knot

            ! Population values of parameters:
            !! Original settings:
            !! mueta0: 102, mueta1: -5, mueta2: -2.6, mug: 5.5
            !! psi00: 25, psi01: 1.5, psi02: 1.5
            !!            psi11: 1.0, psi12: 0.3
            !!                        psi22: 1.0

            !! Reparameterized settings:
            !! mueta0s: 74.5, mueta1s: -3.8, mueta2s: 1.2, mug: 5.5
            !! psi0s0s: 71.750, psi0s1s: 5.075, psi0s2s: -1.925
            !!                  psi1s1s: 0.650, psi1s2s: 0.000
            !!                                  psi2s2s: 0.350
            
            !! theta: 1.0
            eta0s@62.590;
            eta1s@0.557;
            eta2s@0.359;

            eta0s WITH eta1s@4.370;
            eta0s WITH eta2s@-1.485;
            eta1s WITH eta2s@0.052;

            [eta0s@73.175];
            [eta1s@-3.944];
            [eta2s@1.263];

            Y1-Y10@1.024;
            [Y1-Y10@0];


MODEL CONSTRAINT: 
            ! In the model constraint, we have 3 parts:
            ! Part I: Specify extra parameters other than those related to transformed LVs
            new(c1mug*3.552 c2mug*5.545);
            
            ! Part III: Define factor loadings for the 2nd and 3rd LVs with
            ! definition variables
            c1L11 = T1 - c1mug;
            c1L12 = T2 - c1mug;
            c1L13 = T3 - c1mug;
            c1L14 = T4 - c1mug;
            c1L15 = T5 - c1mug;
            c1L16 = T6 - c1mug;
            c1L17 = T7 - c1mug;
            c1L18 = T8 - c1mug;
            c1L19 = T9 - c1mug;
            c1L110 = T10 - c1mug;

            c1L21 = sqrt((T1 - c1mug)^2);
            c1L22 = sqrt((T2 - c1mug)^2);
            c1L23 = sqrt((T3 - c1mug)^2);
            c1L24 = sqrt((T4 - c1mug)^2);
            c1L25 = sqrt((T5 - c1mug)^2);
            c1L26 = sqrt((T6 - c1mug)^2);
            c1L27 = sqrt((T7 - c1mug)^2);
            c1L28 = sqrt((T8 - c1mug)^2);
            c1L29 = sqrt((T9 - c1mug)^2);
            c1L210 = sqrt((T10 - c1mug)^2);

            c2L11 = T1 - c2mug;
            c2L12 = T2 - c2mug;
            c2L13 = T3 - c2mug;
            c2L14 = T4 - c2mug;
            c2L15 = T5 - c2mug;
            c2L16 = T6 - c2mug;
            c2L17 = T7 - c2mug;
            c2L18 = T8 - c2mug;
            c2L19 = T9 - c2mug;
            c2L110 = T10 - c2mug;

            c2L21 = sqrt((T1 - c2mug)^2);
            c2L22 = sqrt((T2 - c2mug)^2);
            c2L23 = sqrt((T3 - c2mug)^2);
            c2L24 = sqrt((T4 - c2mug)^2);
            c2L25 = sqrt((T5 - c2mug)^2);
            c2L26 = sqrt((T6 - c2mug)^2);
            c2L27 = sqrt((T7 - c2mug)^2);
            c2L28 = sqrt((T8 - c2mug)^2);
            c2L29 = sqrt((T9 - c2mug)^2);
            c2L210 = sqrt((T10 - c2mug)^2);
 OUTPUT:    SAMPSTAT TECH1 TECH8;